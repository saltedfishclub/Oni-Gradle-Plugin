plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.4.10'
}

group 'cc.sfclub.oni'
version "1.3"
repositories {
    mavenCentral()
    jcenter()
}
gradlePlugin {
    plugins {
        linker {
            id = "cc.sfclub.oni.linker"
            implementationClass = "io.ib67.oni.Linker"
            displayName= "Oni Linker"
            description= "A gradle plugin that produces a config file automatically for oni-bukkit-bootstrap to do something (e.g solve/download dependencies) for your bukkit plugins."
        }
    }
}
pluginBundle {
    website = 'https://github.com/saltedfishclub/oni-gradle-plugin'
    vcsUrl = 'https://github.com/saltedfishclub/oni-gradle-plugin'
    tags = ['bukkit', 'minecraft']
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"
    gradleApi()
}
publishing{
    publications{
        artifact(MavenPublication) {
            groupId = "cc.sfclub.oni"
            artifactId = "Linker"
            version = project.version

            from components.kotlin
        }
    }
    repositories{
        maven {
            url = "/tmp/repo"
            name = "TmpLocal"
        }
        maven {
            Properties localConf = new Properties()
            if(file("${System.getProperty("user.home")}/.gradle/gradle.properties").exists()) localConf.load(new FileReader("${System.getProperty("user.home")}/.gradle/gradle.properties"))
            def baseUrl="https://repo.sfclub.cc"
            def releasesRepoUrl = "$baseUrl/releases"
            def snapshotsRepoUrl = "$baseUrl/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            name = "SFOfficial"
            credentials {
                username = localConf.getProperty("SF_REPO_ALIAS")
                password = localConf.getProperty("SF_REPO_TOKEN")
            }
        }
    }
}
